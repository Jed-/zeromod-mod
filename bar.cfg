delbar
addbar "Bud"
chatlog = "chat.txt"
onjoin = [ // cn
	cn = $arg1
	joinstr = (concatword "[join] " (getclientcolorname $cn) " has joined")
	if (!=s (getcountry $cn) "unknown") [
		joinstr = (concatword $joinstr " from ")
	]
	if (!=s (gettown $cn) "unknown") [
		joinstr = (concatword $joinstr (gettown $cn) ", ")
	]
	if (!=s (getregion $cn) "unknown") [
		joinstr = (concatword $joinstr (getregion $cn) ", ")
	]
	if (!=s (getcountry $cn) "unknown") [
		joinstr = (concatword $joinstr (getcountry $cn))
	]
	logto $chatlog $joinstr
]
onleave = [ // cn
	cn = $arg1
	leavestr = (concatword "[leave] " (getclientcolorname $cn) " has left")
	logto $chatlog $leavestr
]
onrename = [ // cn newname
	cn = $arg1
	newname = $arg2
	renamestr = (concatword "[rename] " (getclientcolorname $cn) " is now known as " $newname)
	logto $chatlog $renamestr
]
onsetmaster = [ // cn oldpriv priv hidden
	cn = $arg1
	oldpriv = $arg2
	priv = $arg3
	hidden = $arg4
	privs = ["none" "master" "auth" "admin" "root"]
	setmasterstr = (concatword "[priv] " (getclientcolorname $cn))
	if $priv [
		setmasterstr = (concatword $setmasterstr " claimed " (at $privs $priv))
	] [
		setmasterstr = (concatword $setmasterstr " relinquished " (at $privs $oldpriv))
	]
	if $hidden [
		setmasterstr = (concatword $setmasterstr " (hidden)")
	]
	logto $chatlog $setmasterstr
]
onmastermode = [ // cn mm
	cn = $arg1
	mm = $arg2
	mms = ["auth" "open" "veto" "locked" "private" "password"]
	mmstr = (concatword "[info] mastermode is " (at $mms (+ $mm 1)) " (" $mm ")")
	if (>= $cn 0) [
		mmstr = (concatword "[info] " (getclientname $cn) " set mastermode to " (at $mms (+ $mm 1)) " (" $mm ")")
	]
	logto $chatlog $mmstr
]
onkick = [ // kicker victim reason
	kicker = $arg1
	victim = $arg2
	reason = $arg3
	kname = "server"
	if (>= $kicker 0) [
		kname = (getclientcolorname $kicker)
	]
	vname = (getclientcolorname $victim)
	kickstr = (concatword "[kick] " $kname " kicked " $vname)
	if (!=s $reason "") [
		kickstr = (concatword $kickstr " because: " $reason)
	]
	logto $chatlog $kickstr
]
onchat = [ // cn text
	cn = $arg1
	text = $arg2
	logto $chatlog (concatword (getclientcolorname $cn) ": " $text)
	barmen = []
	drunk = 0
	looplist c (listclients) [
		if (&& [isai $c] [isspectator $c]) [
			barmen = (concat $barmen $c)
		]
	]
	loop i (listlen $text) [
		botcn = -1
		looplist c $barmen [
			if (&& [= $botcn -1] [|| [=s (lowerstring (at $text $i)) (lowerstring (getclientname $c))] [=s (lowerstring (getword (at $text $i))) (lowerstring (getclientname $c))]]) [
				botcn = $c
			]
		]
		if (> $botcn -1) [ // "Bud"
			cmd = (lowerstring (getword (at $text (+ $i 1))))
			ishi = 0
			looplist hi $histrings [
				if (&& [! $ishi] [=s $cmd $hi]) [ // "Bud hi"
					ishi = 1
					barhi $botcn $cn
				]
			]
			if (&& $i [! $ishi]) [
				prev = (lowerstring (getword (at $text (- $i 1))))
				looplist hi $histrings [
					if (&& [! $ishi] [=s $hi $prev]) [
						ishi = 1
						barhi $botcn $cn
					]
				]
			]
			if (|| [=s $cmd "menu"] [=s $cmd "list"]) [ // "Bud menu"
				drinks0 = []
				looplist d $drinks [
					drinks0 = (concatword $drinks0 (at $d 0) " ")
				]
				barlist $botcn $cn $drinks0
			]
			drinkidx = -1
			loop i (listlen $drinks) [
				d = (at $drinks $i)
				if (&& [= $drinkidx -1] [|| [=s $cmd (at $d 0)] [=s $cmd (at $d 1)]]) [
					drinkidx = $i
				]
			]
			if (&& [! $drunk] [>= $drinkidx 0]) [ // "Bud beer"
				bardrink $botcn $cn (at (at $drinks $_drinkidx) 0) 1 $cn
				drunk = 1
			]
			if (|| [checknumber $cmd] [=s $cmd "a"] [=s $cmd "an"]) [ // "Bud a"
				num = $cmd
				if (|| [=s $num "a"] [=s $num "an"]) [
					num = 1
				]
				if (< $num 1) [
					num = 1
				]
				drink = (lowerstring (getword (at $text (+ $i 2))))
				drinkidx = -1
				loop i (listlen $drinks) [
					d = (at $drinks $i)
					if (&& [= $drinkidx -1] [|| [=s $drink (at $d 0)] [=s $drink (at $d 1)]]) [
						drinkidx = $i
					]
				]
				if (&& [! $drunk] [>= $drinkidx 0]) [ // "Bud a beer"
					bardrink $botcn $cn (if (> $num 1) [result (at (at $drinks $drinkidx) 1)] [result (at (at $drinks $drinkidx) 0)]) $num $cn
					drunk = 1
				]
			]
			if (|| [=s $cmd "drink"] [=s $cmd "gimme"]) [ // "Bud drink"
				if (|| [checknumber (lowerstring (getword (at $text (+ $i 2))))] [=s (lowerstring (getword (at $text (+ $i 2)))) "a"] [=s (lowerstring (getword (at $text (+ $i 2)))) "an"]) [ // "Bud drink a"
					num = (lowerstring (getword (at $text (+ $i 2))))
					if (|| [=s $num "a"] [=s $num "an"]) [
						num = 1
					]
					if (< $num 1) [
						num = 1
					]
					drink = (lowerstring (getword (at $text (+ $i 3)))) // "Bud drink a beer"
					drinkidx = -1
					loop i (listlen $drinks) [
						d = (at $drinks $i)
						if (&& [= $drinkidx -1] [|| [=s $drink (at $d 0)] [=s $drink (at $d 1)]]) [
							drinkidx = $i
						]
					]
					if (&& [! $drunk] [>= $drinkidx 0]) [
						bardrink $botcn $cn (if (> $num 1) [result (at (at $drinks $drinkidx) 1)] [result (at (at $drinks $drinkidx) 0)]) $num $cn
						drunk = 1
					]
				]
				drink = (lowerstring (getword (at $text (+ $i 2)))) // "Bud drink beer"
				drinkidx = -1
				loop i (listlen $drinks) [
					d = (at $drinks $i)
					if (&& [= $drinkidx -1] [|| [=s $drink (at $d 0)] [=s $drink (at $d 1)]]) [
						drinkidx = $i
					]
				]
				if (&& [! $drunk] [>= $drinkidx 0]) [
					bardrink $botcn $cn (at (at $drinks $drinkidx) 0) 1 $cn
					drunk = 1
				]
			]
			if (|| [=s $cmd "give"] [=s $cmd "offer"]) [ // "Bud give"
				if (|| [checknumber (lowerstring (getword (at $text (+ $i 2))))] [=s (lowerstring (getword (at $text (+ $i 2)))) "a"] [=s (lowerstring (getword (at $text (+ $i 2)))) "an"]) [ // "Bud give a"
					num = (lowerstring (getword (at $text (+ $i 2))))
					if (|| [=s $num "a"] [=s $num "an"]) [
						num = 1
					]
					if (< $num 1) [
						num = 1
					]
					drink = (lowerstring (getword (at $text (+ $i 3)))) // "Bud give a beer"
					drinkidx = -1
					loop i (listlen $drinks) [
						d = (at $drinks $i)
						if (&& [= $drinkidx -1] [|| [=s $drink (at $d 0)] [=s $drink (at $d 1)]]) [
							drinkidx = $i
						]
					]
					if (&& [! $drunk] [>= $drinkidx 0]) [
						bardrink $botcn $cn (if (> $num 1) [result (at (at $drinks $drinkidx) 1)] [result (at (at $drinks $drinkidx) 0)]) $num $cn
						drunk = 1
					]
				] [
					_to = (lowerstring (getword (at $text (+ $i 2))))
					tolist = []
					if (=s $_to "me") [ // "Bud give me"
						tolist = $cn
					]
					if (=s $tolist "") [
						looplist _cn (listclients) [
							if (=s $_to (lowerstring (getclientname $_cn))) [ // "Bud give Jed"
								tolist = (concat $tolist $_cn)
							]
						]
					]
					if (=s $tolist "") [
						looplist _cn (listclients) [
							if (>= (strstr (lowerstring (getclientname $_cn)) $_to) 0) [ // "Bud give J"
								tolist = (concat $tolist $_cn)
							]
						]
					]
					if (!=s $tolist "") [
						if (|| [checknumber (lowerstring (getword (at $text (+ $i 3))))] [=s (lowerstring (getword (at $text (+ $i 3)))) "a"] [=s (lowerstring (getword (at $text (+ $i 2)))) "an"]) [ // "Bud give me a"
							num = (lowerstring (getword (at $text (+ $i 3))))
							if (|| [=s $num "a"] [=s $num "an"]) [
								num = 1
							]
							if (< $num 1) [
								num = 1
							]
							drink = (lowerstring (getword (at $text (+ $i 4)))) // "Bud give me a beer"
							drinkidx = -1
							loop i (listlen $drinks) [
								d = (at $drinks $i)
								if (&& [= $drinkidx -1] [|| [=s $drink (at $d 0)] [=s $drink (at $d 1)]]) [
									drinkidx = $i
								]
							]
							if (&& [! $drunk] [>= $drinkidx 0]) [
								looplist to $tolist [
									bardrink $botcn $to (if (> $num 1) [result (at (at $drinks $drinkidx) 1)] [result (at (at $drinks $drinkidx) 0)]) $num $cn
								]
								drunk = 1
							]
						]
						drink = (lowerstring (getword (at $text (+ $i 3)))) // "Bud give me beer"
						drinkidx = -1
						loop i (listlen $drinks) [
							d = (at $drinks $i)
							if (&& [= $drinkidx -1] [|| [=s $drink (at $d 0)] [=s $drink (at $d 1)]]) [
								drinkidx = $i
							]
						]
						if (&& [! $drunk] [>= $drinkidx 0]) [
							looplist to $tolist [
								bardrink $botcn $to (at (at $drinks $drinkidx) 0) 1 $cn
							]
							drunk = 1
						]
					] [
						drink = (lowerstring (getword (at $text (+ $i 2)))) // "Bud give beer"
						drinkidx = -1
						loop i (listlen $drinks) [
							d = (at $drinks $i)
							if (&& [= $drinkidx -1] [|| [=s $drink (at $d 0)] [=s $drink (at $d 1)]]) [
								drinkidx = $i
							]
						]
						if (&& [! $drunk] [>= $drinkidx 0]) [
							bardrink $botcn $cn (at (at $drinks $drinkidx) 0) 1 $cn
							drunk = 1
						]
					]
				]
			]
		]
	]
]
getclientname = [ // _cn
	_cn = $arg1
	result (at (getclientcolorname $_cn) 0)
]
getword = [ // word
	word = $arg1
	word = (strreplace $word "," "")
	word = (strreplace $word "." "")
	word = (strreplace $word "?" "")
	word = (strreplace $word "!" "")
	word = (strreplace $word ":" "")
	word = (strreplace $word ";" "")
	result $word
]
histrings = ["hi" "hey" "hello"]
drinks = ["beer beers"]
barhi = [ // botcn clientcn
	botcn = $arg1
	cn = $arg2
	str = (concatword "Hey " (at (getclientcolorname $cn) 0) "!")
	fakesay $botcn $str
	logto $chatlog (concatword (getclientcolorname $botcn) ": " $str)
]
barlist = [ // botcn cn list
	botcn = $arg1
	cn = $arg2
	list = $arg3
	str = (concatword "Here's the menu, " (at (getclientcolorname $cn) 0) ": " $list)
	fakesay $botcn $str
	logto $chatlog (concatword (getclientcolorname $botcn) ": " $str)
]
bardrink = [ // botcn tocn item howmuch fromcn
	botcn = $arg1
	tocn = $arg2
	item = $arg3
	howmuch = $arg4
	fromcn = $arg5
	str = (concatword "Here" (? (= $howmuch 1) "'s" " are") (? (= $tocn $fromcn) " your " (concatword " " (? (= $howmuch 1) "a" $howmuch) " ")) $item (? (= $tocn $fromcn) (concatword ", " (at (getclientcolorname $cn) 0)) (concatword " for " (at (getclientcolorname $tocn) 0))) "!")
	fakesay $botcn $str
	logto $chatlog (concatword (getclientcolorname $botcn) ": " $str)
	offer $fromcn $tocn $item $howmuch
]
flipstr = [ // in
	in = $arg1
	out = ""
	loop i (strlen $in) [
		out = (concatword $out (substr $in (- (strlen $in) (+ $i 1)) 1))
	]
	result $out
]
nocolor = [ // in -- needed because of remod's IRC colored messages
	// 0x03 is Â ""
	// 0x0F is É
	// 0x1D is Ù
	// 0x1F is Û
	// 0x02 is Á
	in = $arg1
	numbers = [0 1 2 3 4 5 6 7 8 9]
	if (=s (substr $in 0 1) "Â") [ // is colored?
		if (> (indexof $numbers (substr $in 1 1)) -1) [ // is colored
			in = (substr $in 1 (strlen $in)) // remove 0x03 (Â)
			loop i 2 [ // can have up to 2 color numbers
				if (> (indexof $numbers (substr $in 0 1)) -1) [ // starts with number
					in = (substr $in 1 (strlen $in)) // remove number
				]
			]
		]
	]
	result $in
]
IRC = [ // chan buf
	chan = (unescape $arg1)
	buf = (unescape $arg2)
	barmen = []
	looplist cn (listclients) [
		if (&& [isai $cn] [isspectator $cn]) [
			barmen = (concat $barmen $cn)
		]
	]
	if (listlen $barmen) [
		barcn = (at $barmen 0)
		ircname = (at $buf 0)
		if (&& [=s (substr $ircname 0 1) "<"] [=s (substr $ircname (- (strlen $ircname) 1) 1) ">"]) [ // <name>
			_ircname = (substr $ircname 1 (- (strlen $ircname) 2))
			if (!=s $_ircname "Bud") [
				// FROM REMOD
				if (=s $_ircname "Xbot") [ // Terence's Racing Xtreme bot
					server_name = "Racing Xtreme" // name of current server whose bot's output is being processed
					message = (nocolor (substr $buf (+ (strlen $ircname) 1) (strlen $buf))) // get bot's message
					firstword = (at $message 0)
					if (=s $firstword "CONNECT") [ // CONNECT name(cn) from country [players/max] -- remod makes it a bit hard to parse (and Terence even more)
						player = (at $message 1) // name(cn)
						player_rev = (flipstr $player) // )nc(eman -- must flip string to get last entry
						cn_pos = (strstr $player_rev "(") // find first occurrency of "(" in flipped name -- aka the last in original name
						if (> $cn_pos -1) [ // should always be true
							player_rev = (substr $player_rev (+ $cn_pos 1) (strlen $player_rev)) // eman -- chop flipped name
							player = (flipstr $player_rev) // name -- flip flipped name
						]
						// we have player name now
						country = (substr $message (+ (+ (+ (strlen "CONNECT") (strlen "from")) (strlen (at $message 1))) 3) (strlen $message)) // United States [10/24] -- can't just use (at $message 2) because might be "(United States)"; this somehow returns everything after the second argument
						numplayers = (at $message (- (listlen $message) 1)) // [10/24]
						country = (substr $country 0 (- (strlen $country) (+ (strlen $numplayers) 3))) // United States
						// we have both player name and country now
						zwall (concatword "^f0join: ^f7" $player " on ^f1" $server_name "^f7 from ^f1" $country)
					] [
						if (=s $firstword "DISCONNECT") [ // DISCONNECT name(cn) - Connection time: 00h 00m 00s -- ignore time, only consider name
							player = (at $message 1) // name(cn)
							player_rev = (flipstr $player) // )nc(eman
							cn_pos = (strstr $player_rev "(")
							if (> $cn_pos -1) [
								player_rev = (substr $player_rev (+ $cn_pos 1) (strlen $player_rev)) // eman
								player = (flipstr $player_rev) // name
							]
							// we have player name
							zwall (concatword "^f4leave: ^f7" $player " from ^f1" $server_name)
						] [
							// can be anything else, only consider rename and chat
							player = (at $message 0)
							if (&& [!=s (substr $player (- (strlen $player) 1) 1) ":"] [=s (at $message 1) "is"] [=s (at $message 2) "now"] [=s (at $message 3) "known"] [=s (at $message 4) "as"]) [ // now we are sure we have a rename, name is colored
								player = (nocolor $player) // name(cn)
								player_rev = (flipstr $player) // )nc(eman
								cn_pos = (strstr $player_rev "(")
								if (> $cn_pos -1) [
									player_rev = (substr $player_rev (+ $cn_pos 1) (strlen $player_rev)) // eman
									player = (flipstr $player_rev) // name
								]
								// we have old name
								newname = (nocolor (at $message 5)) // name
								// we have both old and new name
								zwall (concatword $player " is now known as " $newname)
							] [
								if (=s (substr $player (- (strlen $player) 1) 1) ":") [ // name ends with ":", is chat, name is not colored
									chat = (substr $message (+ (strlen $player) 1) (strlen $message))
									// we already have chat
									player = (substr $player 0 (- (strlen $player) 1)) // name(cn)
									player_rev = (flipstr $player) // )nc(eman
									cn_pos = (strstr $player_rev "(")
									if (> $cn_pos -1) [
										player_rev = (substr $player_rev (+ $cn_pos 1) (strlen $player_rev)) // eman
										player = (flipstr $player_rev) // name
										// we have both name and chat
										fakesayas $barcn $player $chat
									]
								]
							]
						]
					]
				] [
					text = (substr $buf (+ (strlen $ircname) 1) (strlen $buf))
//				fakesay $barcn (concatword $_ircname ": " $text)
					fakesayas $barcn $_ircname $text
				]
			]
		]
	] [
		barcn = (at $barmen 0)
		ircname = (at $buf 0)
		if (&& [=s (substr $ircname 0 1) "<"] [=s (substr $ircname (- (strlen $ircname) 1) 1) ">"]) [ // <name>
			_ircname = (substr $ircname 1 (- (strlen $ircname) 2))
			if (!=s $_ircname "Bud") [
				text = (substr $buf (+ (strlen $ircname) 1) (strlen $buf))
				zwall (concatword "^f1[^f7" $chan "^f1]^f7 " (concatword $_ircname ": " $text))
			]
		]
	]
	ircname = (at $buf 0)
	if (=s $ircname "><") [
		event = (at $buf 1)
		nick = $chan
		if (=s $event "JOIN") [
			channel = (at $buf (- (listlen $buf) 1))
			zwall (concatword "^f0join: ^f7" $nick " on ^f1" $channel)
		] [
			if (=s $event "PART") [
				channel = (at $buf 2)
				zwall (concatword "^f4leave: ^f7" $nick " from ^f1" $channel)
			] [
				if (=s $event "QUIT") [
					zwall (concatword "^f4leave: ^f7" $nick " from ^f1IRC network")
				]
			]
		]
	]
]
